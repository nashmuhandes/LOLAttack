version "4.7.1"

enum ELOLAttackType
{
	LOLATK_Doom,
	LOLATK_Heretic,
	LOLATK_Hexen,
	LOLATK_Strife,
}

mixin class LOLAttack
{
	void A_LOLAttack(ELOLAttackType type = LOLATK_Doom)
	{
		if (type == LOLATK_Doom)
		{
			int i = random[rnd_LOLAtk](1, 17);
			switch (i)
			{
			case 0:
			default:
				break;
			case 1:
				A_PosAttack();
				break;
			case 2:
				A_SposAttackUseAtkSound();
				break;
			case 3:
				A_CPosAttack();
				break;
			case 4:
				A_TroopAttack();
				break;
			case 5:
				A_SargAttack();
				break;
			case 6:
				A_HeadAttack();
				break;
			case 7:
				A_BruisAttack();
				break;
			case 8:
				A_SkullAttack();
				break;
			case 9:
				A_PainAttack();
				break;
			case 10:
				A_SkelFist();
				break;
			case 11:
				A_SkelMissile();
				break;
			case 12:
				A_BspiAttack();
				break;
			case 13:
				A_FatAttack1();
				break;
			case 14:
				A_FatAttack2();
				break;
			case 15:
				A_FatAttack3();
				break;
			case 16:
				A_VileAttack();
				break;
			case 17:
				A_CyberAttack();
				break;
			}
		}
	}
}

class LOLZombieMan : ZombieMan replaces ZombieMan
{
	mixin LOLAttack;

	States
	{
	Missile:
		POSS E 10 A_FaceTarget;
		POSS F 8 A_LOLAttack;
		POSS E 8;
		Goto See;
	}
}

class LOLShotgunGuy : ShotgunGuy replaces ShotgunGuy
{
	mixin LOLAttack;

	States
	{
	Missile:
		SPOS E 10 A_FaceTarget;
		SPOS F 10 BRIGHT A_LOLAttack;
		SPOS E 10;
		Goto See;
	}
}

class LOLChaingunGuy : ChaingunGuy replaces ChaingunGuy
{
	mixin LOLAttack;

	States
	{
	Missile:
		CPOS E 10 A_FaceTarget;
		CPOS FE 4 BRIGHT A_LOLAttack;
		CPOS F 1 A_CPosRefire;
		Goto Missile+1;
	}
}

class LOLDoomImp : DoomImp replaces DoomImp
{
	mixin LOLAttack;

	States
	{
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget;
		TROO G 6 A_LOLAttack;
		Goto See;
	}
}

class LOLDemon : Demon replaces Demon
{
	mixin LOLAttack;

	States
	{
	Melee:
		SARG EF 8 Fast A_FaceTarget;
		SARG G 8 Fast A_LOLAttack;
		Goto See;
	}
}

class LOLSpectre : Spectre replaces Spectre
{
	mixin LOLAttack;

	States
	{
	Melee:
		SARG EF 8 Fast A_FaceTarget;
		SARG G 8 Fast A_LOLAttack;
		Goto See;
	}
}

class LOLCacodemon : Cacodemon replaces Cacodemon
{
	mixin LOLAttack;

	States
	{
	Missile:
		HEAD B 5 A_FaceTarget;
		HEAD C 5 A_FaceTarget;
		HEAD D 5 BRIGHT A_LOLAttack;
	}
}

class LOLBaronOfHell : BaronOfHell replaces BaronOfHell
{
	mixin LOLAttack;

	States
	{
	Melee:
	Missile:
		BOSS EF 8 A_FaceTarget;
		BOSS G 8 A_LOLAttack;
		Goto See;
	}
}

class LOLHellKnight : HellKnight replaces HellKnight
{
	mixin LOLAttack;

	States
	{
	Melee:
	Missile:
		BOS2 EF 8 A_FaceTarget;
		BOS2 G 8 A_LOLAttack;
		Goto See;
	}
}

class LOLLostSoul : LostSoul replaces LostSoul
{
	mixin LOLAttack;

	States
	{
	Missile:
		SKUL C 10 BRIGHT A_FaceTarget;
		"####" "#" 0 A_SkullAttack;
		SKUL D 4 BRIGHT A_LOLAttack;
		SKUL CD 4 BRIGHT;
		Goto Missile+2;
	}
}

class LOLPainElemental : PainElemental replaces PainElemental
{
	mixin LOLAttack;

	States
	{
	Missile:
		PAIN D 5 A_FaceTarget;
		PAIN E 5 A_FaceTarget;
		PAIN F 5 BRIGHT A_FaceTarget;
		PAIN F 0 BRIGHT A_LOLAttack;
		Goto See;
	}
}

class LOLRevenant : Revenant replaces Revenant
{
	mixin LOLAttack;

	States
	{
	Melee:
		SKEL G 0 A_FaceTarget;
		SKEL G 6 A_SkelWhoosh;
		SKEL H 6 A_FaceTarget;
		SKEL I 6 A_LOLAttack;
		Goto See;
	Missile:
		SKEL J 0 BRIGHT A_FaceTarget;
		SKEL J 10 BRIGHT A_FaceTarget;
		SKEL K 10 A_LOLAttack;
		SKEL K 10 A_FaceTarget;
		Goto See;
	}
}

class LOLArachnotron : Arachnotron replaces Arachnotron
{
	mixin LOLAttack;

	States
	{
	Missile:
		BSPI A 20 BRIGHT A_FaceTarget;
		BSPI G 4 BRIGHT A_LOLAttack;
		BSPI H 4 BRIGHT;
		BSPI H 1 BRIGHT A_SpidRefire;
		Goto Missile+1;
	}
}

class LOLFatso : Fatso replaces Fatso
{
	mixin LOLAttack;

	States
	{
	Missile:
		FATT G 20 A_FatRaise;
		FATT H 10 BRIGHT A_LOLAttack;
		FATT IG 5 A_FaceTarget;
		FATT H 10 BRIGHT A_LOLAttack;
		FATT IG 5 A_FaceTarget;
		FATT H 10 BRIGHT A_LOLAttack;
		FATT IG 5 A_FaceTarget;
		Goto See;
	}
}

class LOLArchvile : Archvile replaces Archvile
{
	mixin LOLAttack;

	States
	{
	Missile:
		VILE G 0 BRIGHT A_VileStart;
		VILE G 10 BRIGHT A_FaceTarget;
		VILE H 8 BRIGHT A_VileTarget;
		VILE IJKLMN 8 BRIGHT A_FaceTarget;
		VILE O 8 BRIGHT A_LOLAttack;
		VILE P 20 BRIGHT;
		Goto See;
	}
}

class LOLCyberdemon : Cyberdemon replaces Cyberdemon
{
	mixin LOLAttack;

	States
	{
	Missile:
		CYBR E 6 A_FaceTarget;
		CYBR F 12 A_LOLAttack;
		CYBR E 12 A_FaceTarget;
		CYBR F 12 A_LOLAttack;
		CYBR E 12 A_FaceTarget;
		CYBR F 12 A_LOLAttack;
		Goto See;
	}
}

class LOLSpiderMastermind : SpiderMastermind replaces SpiderMastermind
{
	mixin LOLAttack;

	States
	{
	Missile:
		SPID A 20 BRIGHT A_FaceTarget;
		SPID G 4 BRIGHT A_LOLAttack;
		SPID H 4 BRIGHT A_LOLAttack;
		SPID H 1 BRIGHT A_SpidRefire;
		Goto Missile+1;
	}
}
